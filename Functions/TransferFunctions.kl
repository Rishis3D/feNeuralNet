//
// Developed by Gustavo E. Boehs
//
/**
This transfer functions are relevant for squashing values
in a non linear fashion.

Known issues:
* softmax layer overflows with high values

*/

require Matrix;

/// Hyperbolic tangent sigmoid transfer function
function Scalar[] tansig(io Scalar inputs[]) {
  for(Size i=0; i<inputs.size(); i++){
    inputs[i] = 2/(1+exp(-2*inputs[i]))-1;
  }
  return inputs;
}

/// Hyperbolic logistic sigmoid transfer function
function Scalar[] logsig(io Scalar inputs[]) {
  for(Size i=0; i<inputs.size(); i++){
    inputs[i] = 1 / (1 + exp(-inputs[i]));
  }
  return inputs;
}

/// Softmax transfer function
function Scalar[] softmax(io Scalar input[]) {
  Float64 sum;
  Float64 input64;

  for(Size i=0; i<input.size(); i++){
    input64 = input[i];
    sum = sum + exp(input64);
  }
  for(Size i=0; i<input.size(); i++){
    input64 = input[i];
    input[i] = Scalar(exp(input64)/sum);
  }
  return input;
}

/// Softmax transfer function for matrices.
/// \dfgPresetOmit
function Mat softmax(io Mat input) {
  Scalar newValues[];

  for(Size i=0; i<input.cols(); i++) {
    newValues = newValues + softmax(input.getColumn(i));
  }

  Mat newMat(input.cols(),input.rows());
  newMat.setAll(newValues);
  input = newMat.transpose();

  return input;
}

/// Softmax transfer function for matrices.
/// \dfgPresetOmit
function Scalar softmax(io Scalar input) {
  return 1.0;
}